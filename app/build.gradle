plugins {
    id "java"
    id "java-library"
    id "org.springframework.boot" version "3.0.0"
    id "io.spring.dependency-management" version "1.1.0"
    id "org.openapi.generator" version "6.2.0"
    id "com.diffplug.spotless" version "6.12.0"
}

group = "bidderhub"
version = "0.1"
sourceCompatibility = "17"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":bidderclient")

    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.projectlombok:lombok:1.18.22"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Spring Doc
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.0"

    // Testing
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework:spring-webflux"
    testImplementation "org.mockito:mockito-inline:4.4.0"
    testImplementation "org.awaitility:awaitility:4.2.0"


    // Lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // Mapstruct
    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"
    implementation "org.mapstruct:mapstruct:1.5.3.Final"

    // Tools
    implementation "commons-validator:commons-validator:1.7"

    //    Open Feign
    implementation "io.github.openfeign:feign-jackson:12.1"

    compileOnly "javax.servlet:servlet-api:2.5"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    runtimeOnly "org.postgresql:postgresql"
    implementation "org.flywaydb:flyway-core"
}


tasks.named("test") {
    useJUnitPlatform()
}


sourceSets {
    main {
        java {
            srcDir "build/generated/src/main/java"
            srcDir "build/generated/sources/annotationProcessor/java/main"
        }
    }
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "bidderhub.openapi.api"
    modelPackage = "bidderhub.openapi.model"
    additionalProperties = ["interfaceOnly": "true"]
    configOptions = ["useTags"          : "true",
                     "openApiNullable"  : "false",
                     "dateLibrary"      : "java8",
                     "useBeanValidation": "false"]
}

compileJava.dependsOn "openApiGenerate"

spotless {
    java {
        googleJavaFormat()
        targetExclude "build/generated/**"
    }
}

bootJar {
    archiveFileName = "app.jar"
}